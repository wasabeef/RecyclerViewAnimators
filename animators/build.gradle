plugins {
  id('com.android.library')
  id('org.jetbrains.kotlin.android')
  id('org.gradle.maven-publish')
  id('signing')
}

android {
  namespace 'jp.wasabeef.recyclerview'
  compileSdk COMPILE_SDK_VERSION as int

  defaultConfig {
    minSdkVersion MIN_SDK_VERSION as int
    targetSdkVersion TARGET_SDK_VERSION as int
  }

  publishing {
    singleVariant('release') {
      withSourcesJar()
      withJavadocJar()
    }
  }
}

kotlin {
  jvmToolchain(17)
}

dependencies {
  implementation 'org.jetbrains.kotlin:kotlin-stdlib'
  implementation 'androidx.recyclerview:recyclerview:1.3.2'
}

def sonatypeUsername = rootProject.hasProperty('sonatypeUsername') ? rootProject.sonatypeUsername : ''
def sonatypePassword = rootProject.hasProperty('sonatypePassword') ? rootProject.sonatypePassword : ''

group = 'jp.wasabeef'
version = VERSION_NAME

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        def artifact = 'recyclerview-animators'
        artifactId = artifact

        afterEvaluate {
          from components.release
        }

        pom {
          def siteUrl = 'https://github.com/wasabeef/recyclerview-animators'
          def gitUrl = 'https://github.com/wasabeef/recyclerview-animators.git'

          name = artifact
          description = 'Which provides simple Item animations to RecyclerView items'
          url = siteUrl
          licenses {
            license {
              name = 'The Apache Software License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }
          developers {
            developer {
              id = 'wasabeef'
              name = 'Wasabeef'
              email = 'dadadada.chop@gmail.com'
            }
          }
          scm {
            connection = gitUrl
            developerConnection = gitUrl
            url = siteUrl
          }
        }
      }
    }

    repositories {
      maven {
        def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
        def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
        url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        credentials {
          username = "${sonatypeUsername}"
          password = "${sonatypePassword}"
        }
      }
    }
  }
}

signing {
  sign publishing.publications
}
